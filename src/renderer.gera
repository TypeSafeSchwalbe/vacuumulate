
mod vacuumulate::renderer

use vacuumulate::res
use vacuumulate::gamestate::DATA_HIGHSCORE

use std::(arr, iter, str, opt)
use obem::gfx::(Shader, Surface, Mesh)
use obem::(mat, vec, data)

pub val CAMERA_POSITION = [0.0, 10.0, 10.0]
pub val CAMERA_LOOK_AT = [0.0, 0.0, -3.0]
pub val CAMERA_UP = [0.0, 1.0, 0.0]

pub proc create() {
    return {
        object_camera = mat::iden4x4(),
        shader = Shader::new(
            "
                attribute vec3 pos;
                attribute vec2 uv;
                varying vec2 f_uv;

                uniform mat4 u_view_projection;
                uniform mat4 u_model;

                void main(void) {
                    f_uv = uv;
                    gl_Position = u_view_projection * u_model * vec4(pos, 1.0);
                }
            ",
            "
                precision mediump float;
                varying vec2 f_uv;
                
                uniform sampler2D u_tex; 

                void main(void) {
                    gl_FragColor = texture2D(u_tex, f_uv);
                }
            "
        )
    }
}

proc set_object_camera(renderer) {
    val fov = std::math::PI * 0.3
    val aspect_ratio = (Surface::main() .> width() |> as_flt())
        / (Surface::main() .> height() |> as_flt())
    val camera_matrix = mat::perspective(fov, aspect_ratio, 0.01, 1000.0)
       |> mat::mul(mat::look_at(CAMERA_POSITION, CAMERA_LOOK_AT, CAMERA_UP))
    renderer.object_camera = camera_matrix
}

proc render_object(renderer, model_matrix, texture, mesh, depth_test) {
    renderer.shader .> uniform_mat("u_model", model_matrix)
    renderer.shader .> uniform_tex("u_tex", texture)
    renderer.shader .> uniform_mat("u_view_projection", renderer.object_camera)
    Surface::main() 
        .> draw_mesh(mesh, [3, 2], renderer.shader, depth_test)
}

proc as_ndc(pixel_pos) {
    return pixel_pos
        |> vec::div([
            Surface::main() .> width() |> as_flt(),
            Surface::main() .> height() |> as_flt()
        ])
        |> vec::mul([2.0, 2.0]) |> vec::sub([1.0, 1.0])
        |> vec::mul([1.0, -1.0])
}

proc render_sprite(renderer, position, size, texture, src_pos, src_size) {
    // this could be optimized by using a singular mesh with 
    // a model matrix and an orthographic projection matrix
    val top_left = position |> as_ndc()
    val bottom_right = position |> vec::add(size) |> as_ndc()
    val uv_bottom_left = src_pos
        |> vec::div([
            texture .> width() |> as_flt(), texture .> height() |> as_flt()
        ])
    val uv_top_right = src_pos
        |> vec::add(src_size)
        |> vec::div([
            texture .> width() |> as_flt(), texture .> height() |> as_flt()
        ])
    val mesh = Mesh::new(
        [
            top_left[0],     top_left[1],     0.0, uv_bottom_left[0], uv_top_right[1], 
            bottom_right[0], top_left[1],     0.0, uv_top_right[0],   uv_top_right[1],
            top_left[0],     bottom_right[1], 0.0, uv_bottom_left[0], uv_bottom_left[1],
            bottom_right[0], bottom_right[1], 0.0, uv_top_right[0],   uv_bottom_left[1]
        ], [
            2, 1, 0,
            3, 1, 2
        ]
    )
    renderer.shader .> uniform_mat("u_model", mat::iden4x4())
    renderer.shader .> uniform_tex("u_tex", texture)
    renderer.shader .> uniform_mat("u_view_projection", mat::iden4x4())
    Surface::main() 
        .> draw_mesh(mesh, [3, 2], renderer.shader, false)
}

val FONT_CHARS
    = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789!.,:; ()"

proc render_text(renderer, position, size, text) {
    val char_width = size * 7.0 / 9.0
    val char_padding = size * 0.0 / 9.0
    val char_offset = char_width + char_padding
    0..length(text) |> iter::for_each(|i| {
        val c = text |> str::at(i)
        val c_font_idx = FONT_CHARS
            |> str::find(c) |> iter::next()
            |> opt::unwrap_or(length(FONT_CHARS))
        val drawn_pos = position
            |> vec::add([as_flt(i) * char_offset, 0.0])
        val drawn_size = [char_width, size]
        val src_pos = [as_flt(c_font_idx) * 8.0, 0.0]
        val src_size = [7.0, 9.0]
        renderer |> render_sprite(
            drawn_pos, drawn_size, res::FONT_TEXTURE .> get(), src_pos, src_size
        )
    })
}

proc rendered_text_width(size, text) {
    val char_width = size * 7.0 / 9.0
    val char_padding = size * 0.0 / 9.0
    return as_flt(length(text)) * char_width
        + as_flt(length(text) - 1) * char_padding
}

proc render_room(renderer) {
    renderer |> render_object(
        mat::iden4x4()
            |> mat::scale([10.0, 10.0, 10.0, 1.0])
            |> mat::mul(mat::translation([0.0, -0.01, 0.0])),
        res::ROOM_TEXTURE .> get(), res::ROOM_MODEL .> get(),
        true
    )
}

proc render_robot(renderer, robot) {
    val model_matrix = mat::translation(robot.position)
        |> mat::rotate_y(-robot.rotation)
        |> mat::rotate_y(std::math::PI)
    renderer |> render_object(
        model_matrix, res::ROBOT_TEXTURE .> get(), res::ROBOT_MODEL .> get(),
        true
    )
}

proc render_debris(renderer, debris) {
    val model_matrix = mat::translation(debris.position)
        |> mat::rotate_y(debris.rotation)
    renderer |> render_object(
        model_matrix, debris.texture, res::DEBRIS_MODEL .> get(),
        true
    )
}

proc render_obstacle(renderer, obstacle) {
    val model_matrix = mat::translation(obstacle.position)
        |> mat::rotate_y(obstacle.rotation)
    renderer |> render_object(
        model_matrix, obstacle.texture, obstacle.model,
        true
    )
}

proc render_smoke(renderer, smoke) {
    val scale_factor = 1.0 + smoke.position[1]
    val model_matrix = mat::translation(smoke.position)
            |> mat::scale([scale_factor, scale_factor, scale_factor, 1.0])
    renderer |> render_object(
        model_matrix, res::SMOKE_TEXTURE .> get(), res::SMOKE_MODEL .> get(),
        false
    )
}

proc render_explosion(renderer, explosion) {
    val model_matrix = mat::translation(explosion.position)
            |> mat::scale([explosion.size, explosion.size, explosion.size, 1.0])
    renderer |> render_object(
        model_matrix,
        res::EXPLOSION_TEXTURE .> get(), res::EXPLOSION_MODEL .> get(),
        false
    )
}

proc render_score(renderer, score) {
    val score_text = "hi _   _"
        |> str::fmt([data::get(DATA_HIGHSCORE), score |> as_str()])
    val screen_height = Surface::main() .> height() |> as_flt()
    renderer |> render_text(
        [50.0, 50.0], screen_height / 30.0, score_text)
}

proc render_menu(renderer, menu) {
    val screen_width = Surface::main() .> width() |> as_flt()
    val screen_height = Surface::main() .> height() |> as_flt()
    val title_size = screen_height / 10.0
    val prompt_size = screen_height / 40.0
    val padding_size = screen_height / 10.0
    val menu_size = title_size + padding_size + prompt_size
    val title_width = rendered_text_width(title_size, menu.title)
    val title_pos = [
        (screen_width - title_width) / 2.0, 
        (screen_height - menu_size) / 2.0
    ]
    renderer |> render_text(title_pos, title_size, menu.title)
    val prompt_width = rendered_text_width(prompt_size, menu.prompt)
    val prompt_pos = [
        (screen_width - prompt_width) / 2.0,
        (screen_height - menu_size) / 2.0 + title_size + padding_size
    ]
    renderer |> render_text(prompt_pos, prompt_size, menu.prompt)
}

pub proc render(renderer, gamestate) {
    val background = [95.0, 87.0, 79.0, 255.0]
        |> vec::scale(1.0 / 255.0)
    Surface::main() .> clear_color(background)
    renderer |> set_object_camera()
    renderer |> render_room()
    renderer |> render_robot(gamestate.robot)
    gamestate.debris
        |> arr::iter()
        |> iter::for_each(|debris| renderer |> render_debris(debris))
    gamestate.obstacles
        |> arr::iter()
        |> iter::for_each(|obstacle| renderer |> render_obstacle(obstacle))
    gamestate.smoke
        |> arr::iter()
        |> iter::for_each(|smoke| renderer |> render_smoke(smoke))
    case gamestate.explosion {
        #some explosion -> renderer |> render_explosion(explosion)
    } else {}
    renderer |> render_score(gamestate.score)
    renderer |> render_menu(gamestate.menu)
}